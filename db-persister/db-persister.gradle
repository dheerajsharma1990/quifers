apply plugin: 'java'

dependencies {
    compile project(':domain')
    compile group: 'org.hibernate', name: 'hibernate-core', version: '4.3.6.Final'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.12'

    testCompile group: 'com.h2database', name: 'h2', version: '1.4.187'
}

configurations {
    testOutput.extendsFrom(testCompile)
}


task testJar(type: Jar, dependsOn: testClasses) {
    from sourceSets.test.output
    classifier = 'test'
}

artifacts {
    testOutput testJar
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
        parallel 'false'
    }
    forkEvery = 0
    maxParallelForks = 1
    testLogging {
        events('failed')
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}